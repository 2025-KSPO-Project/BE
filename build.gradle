plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.kspo'
version = '0.0.1-SNAPSHOT'
description = 'care-fit'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// QueryDSL Version
ext {
    querydslVersion = "5.0.0"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    /*WEB*/
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    /*FeignClient*/
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.0'

    /*LOMBOK*/
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    /*DATA*/
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.11.0'

    /*QUERY-DSL*/
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"


    /*FILES*/
    implementation 'org.apache.commons:commons-compress:1.22'

    /*JSON*/
    implementation 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.14.2'

    /*UTIL*/
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.google.code.gson:gson'

    /*OPS*/
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
//    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    /*SECURITY*/
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    /*TEST*/
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.rest-assured:rest-assured'

    /*SWAGGER*/
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'


}

// Querydsl 설정
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

// gradle clean 시에 Qclass 디렉토리 삭제
clean {
    delete file(generated)
}


tasks.named('test') {
    useJUnitPlatform()
}